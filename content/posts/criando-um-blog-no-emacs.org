#+title: Criando um blog no Emacs

Um blog √© nada mais do que uma por√ß√£o de p√°ginas *html* est√°ticas, no entanto escrever texto em *html* puro n√£o √© uma experi√™ncia agr√°davel. Usu√°rios de [[https://www.gnu.org/software/emacs/][Emacs]] est√£o acostumados a utilizar o [[https://orgmode.org/https://orgmode.org/][Org Mode]] para a cria√ß√£o de documentos e notas. O Org Mode suporta tudo o que √© necess√°rio para posts de um blog: texto com marca√ß√£o, imagens e realce de sintaxe para blocos de c√≥digo. Seria √≥timo poder escrever os posts do seu blog no conforto do Emacs e √© justamente isso que o sistema de publica√ß√£o do Org Mode proporciona, publicar arquivos *.org* como p√°ginas *html*.

O intuito deste post √© demonstrar os passos necess√°rios para a configura√ß√£o do sistema de publica√ß√£o do Org Mode visando a cria√ß√£o de um blog. Para facilitar o entendimento, algumas configura√ß√µes ser√£o suprimidas, voc√™ pode encontrar o exemplo completo do blog Vida em 8 Bits no meu [[https://github.com/mauricio-ms/mauricio-ms.github.io][GitHub]].

O primeiro passo √© carregar o sistema de publica√ß√£o:

#+begin_src emacs-lisp
  (require 'ox-publish)
#+end_src

A configura√ß√£o √© toda realizada atrav√©s da vari√°vel =org-publish-project-alist=:

#+begin_src emacs-lisp
  (setq org-publish-project-alist
      (list '("blog:main"
              :base-directory "./content"
              :base-extension "org"
              :publishing-directory "./public"
              :publishing-function org-html-publish-to-html)
            '("blog:assets"
              :base-directory "./assets"
              :base-extension "css\\|js\\|png\\|jpg\\|gif\\|pdf\\|mp3\\|ogg\\|woff2\\|ttf"
              :publishing-directory "./public"
              :recursive t
              :publishing-function org-publish-attachment)))
#+end_src

neste exemplo podemos ver a configra√ß√£o de dois items - =blog:main= e =blog:assets= - que representam as p√°ginas do blog e os /assets/ como imagens e arquivos CSS. Cada item realiza a configura√ß√£o por meio de atributos:
- =:base-directory= - Define o diret√≥rio de origem onde os arquivos est√£o localizados no seu sistema de arquivos.
- =:base-extension= - Define a extens√£o dos arquivos do diret√≥rio de origem.
- =:publishing-directory= - Define o diret√≥rio de publica√ß√£o, isto √©, onde os arquivos processados ser√£o publicados.
- =:publishing-function= - Define a fun√ß√£o respons√°vel pelo processamento dos arquivos.
- =:recursive= - Define se sub-diret√≥rios do diret√≥rio de origem devem ser considerados para processamento.
  
O exemplo define fun√ß√µes de publica√ß√£o diferentes para cada item:
- =org-html-publish-to-html= - Converte arquivos *.org* em p√°ginas *html*.
- =org-publish-attachment= - Copia arquivos sem qualquer modifica√ß√£o.

Por fim, basta uma √∫nica linha de c√≥digo para realizar a publica√ß√£o de todos os projetos configurados na vari√°vel =org-publish-project-alist=:

#+begin_src emacs-lisp
  (org-publish-all t)
#+end_src

De modo a facilitar a publica√ß√£o √© aconselh√°vel colocar todo este c√≥digo em um √∫nico arquivo *.el* e execut√°-lo a partir de um /shell script/:

#+begin_src sh
  #!/bin/sh
  emacs -Q --script build-site.el
#+end_src

o par√¢metro =-Q= executa o Emacs sem carregar arquivos de configura√ß√£o, √© mais r√°pido e garante que ir√° funcionar em qualquer computador.

Com isso, o blog est√° pronto. Agora basta escrever seus posts como documentos Org Mode. A p√°gina inicial deve ser nomeada *index.org* e links para outras p√°ginas podem ser criados por meio de links no formato do Org-mode:

#+begin_src literal
  [[file:other-page.org][Other Page]]
#+end_src

o mesmo vale para recursos como imagens:

#+begin_src literal
  [[file:img/some-image.jpg]]
#+end_src

Para facilitar a adi√ß√£o de links, basta executar =C-c c-l= e interativamente informar o link e a descri√ß√£o.

* Adicionando cabe√ßalho e rodap√©

Podemos querer modificar o estilo do blog como um todo para adicionar cabe√ßalho e rodap√© em todas as p√°ginas. Para isso podemos sobreescrever o /exporter/ de *html* do Org Mode:

#+begin_src emacs-lisp
  (require 'cl-lib)
  (use-package esxml
    :pin "melpa-stable"
    :ensure t)

  (org-export-define-derived-backend 'site-html 'html
                                     :translate-alist
                                     '((template . org-html-template)))

  (defun org-html-template (contents info)
    (generate-page (org-export-data (plist-get info :title) info)
                   contents
                   info
                   :publish-date (org-export-data
                                  (org-export-get-date info "%B %e, %Y") info)))

  (cl-defun generate-page (title
                           content
                           info
                           &key
                           (publish-date))
    (concat
     "<!DOCTYPE html>"
     (sxml-to-xml
      `(html (@ (lang "en"))
             (head
              (meta (@ (charset "utf-8")))
              (meta (@ (author "Vida em 8 Bits - Maur√≠cio Mussatto Scopel")))
              (meta (@ (name "viewport")
                       (content "width=device-width, initial-scale=1, shrink-to-fit=no")))
              (link (@ (rel "icon") (type "image/png") (href "/img/favicon.png"))) 
              (link (@ (rel "stylesheet") (href ,(concat dw/site-url "/css/code.css"))))
              (link (@ (rel "stylesheet") (href ,(concat dw/site-url "/css/site.css"))))
              (title ,(concat title " - Vida em 8 Bits")))
             (body (site-header)
                   (div (@ (class "container"))
                        (h1 "Ol√°, leitores do Vida em 8 Bits!")
                        (div (@ (id "content"))
                             ,content))
                   (site-footer))))))
#+end_src

a fun√ß√£o =generate-page= est√° super simplificada para dar o entendimento do essencial, cont√©m alguns par√¢metros n√£o utilizados para demonstrar como dados podem ser extra√≠dos para serem utilizados na gera√ß√£o do *html*. Al√©m disso, demonstra como adicionar scripts ou arquivos CSS. As fun√ß√µes =site-header= e =site-buffer= foram omitidas por quest√µes de brevidade, mas como seus nomes indicam, s√£o fun√ß√µes para gerar o cabe√ßalho e o rodap√© do blog, devem retornar templates dos elementos *html* no formato:

#+begin_src emacs-lisp
    (list `(element1)
          `(element2))
#+end_src

* Melhorando o visual

Para um bom visual do seu blog, a instala√ß√£o do pacote =htmlize= √© essencial se voc√™ deseja utilizar blocos de c√≥digo. O restante do =html= gerado pode ser customizado por meio de vari√°veis:

#+begin_src emacs-lisp
  (setq org-publish-use-timestamps-flag t
        org-export-with-section-numbers nil
        org-export-use-babel nil
        org-export-with-smart-quotes t
        org-export-with-sub-superscripts nil
        org-export-with-tags 'not-in-toc
        org-html-htmlize-output-type 'css
        org-html-prefer-user-labels t
        org-html-link-use-abs-url t
        org-html-link-org-files-as-html t
        org-html-html5-fancy t
        org-html-self-link-headlines t
        org-export-with-toc nil
        make-backup-files nil)
#+end_src

para compreender o objetivo de cada vari√°vel execute =M-x describe-variable= ou use o atalho =C-h v= e digite o nome da vari√°vel.

* Organizando o download de pacotes

Para evitar que pacotes obtidos na execu√ß√£o do script sejam instalados no seu diret√≥rio de configura√ß√£o principal do Emacs, basta atualizar o valor da vari√°vel =package-user-dir= para apontar para um diret√≥rio oculto como por exemplo =.packages=:

#+begin_src emacs-lisp
  (require 'package)
  (setq package-user-dir (expand-file-name "./.packages"))

  (add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/"))
  (add-to-list 'package-archives '("melpa-stable" . "https://stable.melpa.org/packages/"))

  ;; Initialize the package system
  (package-initialize)
  (unless package-archive-contents
    (package-refresh-contents))

  ;; Install use-package
  (unless (package-installed-p 'use-package)
    (package-install 'use-package))
  (require 'use-package)  
#+end_src

* Acessando o blog

Para acessar seu blog localmente voc√™ pode utilizar o pacote [[https://github.com/skeeto/emacs-web-server][simple-httpd]] para subir um servidor local, ap√≥s a instala√ß√£o do pacote basta executar =M-x httpd-serve-directory=, selecionar o diret√≥rio de publica√ß√£o e ent√£o acessar o blog via http://localhost:8080.

* Criando a listagem de posts

Todo blog tem uma p√°gina listando todos os posts pela data mais recente. Bem, isso √© simples de resolver, basta automatizar a gera√ß√£o de uma p√°gina *.org* contendo a listagem dos posts e o sistema de publica√ß√£o tomar√° conta do restante. O c√≥digo em Emacs Lisp abaixo resolve isso de forma simples:

- Obt√©m todos os arquivos no diret√≥rio de posts do blog;
- Para cada post:
  - Insere uma linha com o respectivo link no qual a descri√ß√£o √© o t√≠tulo extra√≠do do documento *.org*;
  - Insere uma linha identificando o autor do blog e a data de publica√ß√£o, obtida pela data de commit do arquivo.

#+begin_src emacs-lisp
  (require 'org)
  (require 'vc-git)

  (defun get-commit-date (filepath)
    (string-trim-right
     (with-output-to-string
       (with-current-buffer standard-output
         (vc-git-command t nil nil "log" "--max-count=1" "--date=short" "--format=%cd" filepath)))))

  (defun parse-date (date)
    "Parse DATE to dd de mm, yyyy."
    (format-time-string "%d de %B, %Y" date))

  (with-temp-file "content/blog.org"
    (let ((posts-folder "./content/posts/"))
      (seq-do
       (lambda (post)
         (insert (format "[[../%s][%s]]\n\n"
                         (car (string-split post ".org"))
                         (org-get-title (concat posts-folder post))))
         (insert (format "%s por Maur√≠cio Mussatto Scopel\n"
                         (parse-date
                          (date-to-time
                           (get-commit-date (concat posts-folder post)))))))
       (directory-files posts-folder nil ".org"))))
#+end_src

o leitor atento pode ter notado que este script n√£o considera ordena√ß√£o, √© isso mesmo, ordena√ß√£o n√£o √© uma preocupa√ß√£o enquanto o seu blog n√£o tem mais de 1 post üòÖ.

At√© o pr√≥ximo post!
